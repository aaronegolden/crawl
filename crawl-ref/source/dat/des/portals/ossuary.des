##############################################################################
#
# The Ossuary, a Tomb en miniature (Zaba)
#
#
# The Ossuary tries to pose an unusual early challenge: many traps and slow
# monsters, making the player choose between pressing on towards the loot or
# leaving the vault.
#
# Flavour: Mini versions of Tomb and otherwise crypt like.
#
##############################################################################

{{
function ossuary_portal(e)
  local timeout_turns = crawl.random_range(600, 800)

  local messager =
    timed_msg {
      initmsg = { "You hear the hiss of flowing sand.",
                  "There is an entrance to an ossuary on this level. "
                  .. "Hurry and find it before the portal is buried!" },
      finalmsg = "The hiss of flowing sand is almost imperceptible now.",

      verb = 'hiss',
      -- This is always preceded by an article, so...
      noisemaker = 'avalanche of sand',
      ranges = {
        { 5000, 'slow ' },  { 4000, '' },
        { 2500, 'brisk ' }, { 1500, 'quick ' },
        { 0, 'rapid ' }
      }
    }

  e.lua_marker('O',
      timed_marker {
        disappear = "The hiss of flowing sand is gone.",
        entity = 'staircase',
        turns = timeout_turns,
        single_timed = true,
        floor = "expired_portal",
        feat_tile = "dngn_portal_ossuary_gone",
        msg = messager })
  e.tags("uniq_ossuary no_monster_gen chance_ossuary")
  e.chance(500)
  e.kfeat("O = enter_ossuary")
  e.colour("' = yellow")
  e.ftile("' = floor_sandstone")
  e.tile("c = wall_tomb")
end

-- Use the following line in destination maps after all SUBSTs
-- : ossuary_setup_features(_G)
-- Note that the function also defines < to be the exit portal.
function ossuary_setup_features(e)
  e.kfeat("< = exit_ossuary")
end

function callback.badplayer_boxes_open_up(data, triggerable, triggerer, marker,
                                          ev)
  if not data.triggered then
    data.triggered = true
    for slave in iter.slave_iterator("disappearing_wall", 1) do
      dgn.terrain_changed(slave.x, slave.y, "floor", false)
    end
    crawl.mpr("There is a click, and some walls slide away!", 'warning')
  end
end
}}

#### Portal entry vaults.
default-depth: D:4-8

NAME:   enter_ossuary_1
TAGS:   transparent
SUBST:  . = . ':7
: ossuary_portal(_G)
MAP
.....
.'''.
.'O'.
.'''.
.....
ENDMAP

NAME:    enter_ossuary_2
TAGS:    transparent
SHUFFLE: C+
SUBST:   C = c
: ossuary_portal(_G)
MAP
.......
.cc+cc.
.c'''c.
.C'O'C.
.c'''c.
.ccCcc.
.......
ENDMAP

NAME:   enter_ossuary_3
TAGS:   transparent
SUBST:  . = ' .
MONS:   rat zombie / jackal zombie
MONS:   kobold zombie / goblin zombie / hobgoblin zombie
MONS:   orc zombie / gnoll zombie / human zombie
SUBST:  . = .:100 11 2 3
: ossuary_portal(_G)
MAP
...
.O.
...
ENDMAP

NAME:   enter_ossuary_4
TAGS:   transparent
COLOUR: 1 = yellow, x = white
MONS:   rat zombie / quokka zombie / goblin zombie
FTILE:  1 = floor_sandstone, x = floor_sandstone, O = floor_sandstone
: ossuary_portal(_G)
MAP
.........
.ccccccc.
.c''O''c.
.c'''''c.
.c'c'c'c.
.c1c'c1c.
.ccc'ccc.
....'....
ENDMAP

NAME:   enter_ossuary_5
TAGS:   transparent
COLOUR: c = white, + = white, ' = yellow
: ossuary_portal(_G)
MAP
   ...
  ..c..
 ..cc+..
..cc'cc..
.cc'O'cc.
..cc'cc..
 ..ccc..
  ..c..
   ...
ENDMAP

NAME:   gup_ossuary_entry_pyramid
TAGS:   transparent
KMONS:  O = mummy
NSUBST: ' = 3:~ / *:'
: ossuary_portal(_G)
MAP
     ...
    ..c..
   ..ccc..
  ..ccOcc..
 ..cc'''cc..
..cc'''''cc..
.ccccc+ccccc.
.............
ENDMAP

NAME: nicolae_ossuary_entry_catacombs
FTILE: O1s+ABC = floor_sandstone
COLOUR: 1s = yellow
NSUBST: A = + / c, B = + / c, C = + / c, s = 1:O / 4:1 / *:1.....
KMONS: O1 = orc skeleton / orc zombie w:5 / elf skeleton / elf zombie w:5 / \
            human skeleton / human zombie w:5
: ossuary_portal(_G)
MAP
ccccccccc
+'1'csAsc
ccc+c+cAc
cs+'''+sc
ccc'''ccc
cs+'''+sc
cCc+c+cBc
csCscsBsc
ccccccccc
ENDMAP

NAME: nicolae_ossuary_entry_false_entrances
NSUBST: O = O / >
SHUFFLE: AB/AB/AB/BA
SUBST: A = m, B = c
FTILE: Om> = floor_sandstone
: ossuary_portal(_G)
MAP
   xxxxx
 +ccccccc+
 c'''c'''c
xc'O'A'O'cx
xc'''B'''cx
xccABBBAccx
xc'''B'''cx
xc'O'A'O'cx
 c'''c'''c
 +ccccccc+
   xxxxx
ENDMAP

NAME: nicolae_ossuary_entry_giza
TAGS: transparent
NSUBST: M = 1:O / *:z
KMONS: O = mummy
KMONS: z = goblin zombie / hobgoblin zombie / kobold zombie / orc zombie
FTILE: Oz = floor_sandstone
COLOUR: z = yellow
: ossuary_portal(_G)
MAP
          .
         ...
        ..c..
       ..ccc..
      ..ccMcc..
     ..ccc+ccc..
    ..c.......c..
   ..ccc.....ccc..
  ..ccMcc...ccMcc..
 ..ccc+ccc.ccc+ccc..
.....................
ENDMAP

NAME: nicolae_ossuary_entry_monument
TAGS: transparent
FTILE: G = floor_sandstone
SUBST: - = '.
: ossuary_portal(_G)
MAP
..G..
.'''.
G'O'G
.'''.
..G..
ENDMAP

NAME: nicolae_ossuary_entry_plus
TAGS: transparent
: if crawl.coinflip() then
SUBST: Dd = +, o = O, p = '
: else
SUBST: d = +, D = c, o = '
NSUBST: p = O / '
: end
FTILE: + = floor_sandstone
: ossuary_portal(_G)
MAP
.........
...cDc...
..'cpc'..
.ccc'ccc.
.Dp'o''d.
.ccc'ccc.
..'cpc'..
...cDc...
.........
ENDMAP

NAME: nicolae_ossuary_entry_ruins
TAGS: transparent
FTILE: GO = floor_sandstone
SUBST: c = -c, - = ''., ^ = '..
: ossuary_portal(_G)
MAP
.......
.cc-cc.
.c---c.
.--O--.
.c---c.
.cc-cc.
.......
ENDMAP

NAME: lightli_ossuary_entry_decrepit_ruins
TAGS: transparent ruin
NSUBST: a = 1:' / *:c
: ossuary_portal(_G)
MAP
GcccccG
cc'O'cc
caaaaac
c'''''c
c'''''c
cc'''cc
Gcc'ccG
   @
ENDMAP

NAME: chequers_ossuary_entry_cave
TAGS: transparent
: ossuary_portal(_G)
MAP
xxxxxcccccccxxxxx
xxxxcc'''''ccxxxx
xxxxcG''O''Gcxxxx
xxxxcc'''''ccxxxx
xxxxxccc'cccxxxxx
xxxxxxxc'cxxxxxxx
xxxxxxxcAcxxxxxxx
.................
@@@@@@@@@@@@@@@@@
ENDMAP

#### The portal vaults ####################################

# Reset default depth to prevent random generation of portal vaults.
# Sort destination maps by WEIGHT.
default-depth: Ossuary

###########################################################
# Common: WEIGHT 40
###########################################################

NAME:    ossuary_tomb_1
WEIGHT:  40
ORIENT:  encompass
TAGS:    no_item_gen no_monster_gen no_rotate
SHUFFLE: dwyz
SUBST:   w=2, y=2
NSUBST:  z= 1:1 / *:3
KFEAT:   T = known alarm trap / floor
# loot: 12.5 items
ITEM:    any scroll w:2 / any scroll q:2 w:3 / \
         any potion w:4 / any potion q:2 w:6
MONS:    mummy
MONS:    rat zombie / jackal zombie / adder zombie / goblin zombie / \
         hobgoblin zombie / kobold zombie / big kobold zombie / \
         human zombie / elf zombie / centaur zombie
MONS:    kobold zombie / orc zombie / hobgoblin zombie
KFEAT: Z = scroll shop / scroll shop / distillery shop
: ossuary_setup_features(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxx
x.........<A.........x
x....................x
x...cccccccccccccc...x
x...c.22*c...cddZc...x
x3..c1cccc...cdddc..3x
x..3c........+cccc3..x
x3..cccccc.<.....c..3x
x..3c.zzzc.......c3..x
x3..c.czzc.......c..3x
x...c.cccc.......c...x
x...c............c...x
x...c.ccccTTcccc.c...x
x...c.cyycTTcwwc.c...x
x...c.yyycTTcwww.c...x
x...ccccccTTcccccc...x
x....................x
x.3.3.3..G..G..3.3.3.x
x..3.3..........3.3..x
x....................x
xxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    ossuary_tomb_2
# Generally have rooms full of either monsters or traps.
WEIGHT:  40
ORIENT:  encompass
TAGS:    no_item_gen no_monster_gen no_rotate
KFEAT:   ~ = known arrow trap w:2 / known needle trap
KFEAT:   ^ = known arrow trap / floor w:5
# Number of 1's is 53, number of ~'s is 57.
: if crawl.one_chance_in(10) then
SUBST:   ~ = 1
SUBST:   1 = 1:20 2:10 ~:50 .:30
: else
SHUFFLE: 1~
SUBST:   1 = 1:20 2:10 .:20
SUBST:   ~ = ~:20 .:10
:end
SUBST:   z = 1
NSUBST:  B = 1:+ / *:c
NSUBST:  C = 1:+ / *:c
NSUBST:  D = 1:+ / *:c
NSUBST:  E = 1:+ / *:c
NSUBST:  F = 1:+ / *:c
NSUBST:  G = 1:+ / *:c
NSUBST:  H = 1:+ / *:c
NSUBST:  I = 1:+ / *:c
NSUBST:  J = 1:+ / *:c
NSUBST:  K = 1:+ / *:c
# loot: 12 items
ITEM:    any scroll w:6 / any potion / any potion q:2 w:2
MONS:    adder zombie / hobgoblin zombie / orc zombie / kobold zombie / nothing
MONS:    big kobold zombie / water moccasin zombie / gnoll zombie / nothing
MONS:    mummy
KFEAT: Z = scroll shop / scroll shop / distillery shop
: ossuary_setup_features(_G)
MAP
cccccccccccccccccccccc
c<11H~~~G1111F~~~E111c
c111H~~~G1111F~~~E111c
c111H~~~G1111F~~~E111c
cIIIccccccccccccccDDDc
c~~~c            c~~~c
c~~~c            c~~~c
c~~~c            c~~~c
c~~~c            c~~~c
cJJJc            cCCCc
c111c            c111c
c111c            c111c
c111c            c111c
c111c            c111c
cKKKc            cBBBc
c~~~c            c^^^c
c~~~cccccccccccccc^^^c
c~~~c...cddddc...c.z^c
c~~~+3<3+d**Zn.A.+.^^c
c~~~c...cddddc...c.z<c
cccccccccccccccccccccc
ENDMAP

NAME:    ossuary_tomb_3
WEIGHT:  40
ORIENT:  encompass
TAGS:    no_item_gen no_monster_gen no_rotate no_vmirror
# zombies should be awake, so they march downwards in a line
MONS:    generate_awake orc zombie / generate_awake kobold zombie / \
         generate_awake hobgoblin zombie / generate_awake gnoll zombie
MONS:    mummy
MONS:    orc zombie / kobold zombie / big kobold zombie / hobgoblin zombie
ITEM:    nothing / any scroll w:5 / any potion
KFEAT:   ^ = known alarm trap
KFEAT: Z = scroll shop / scroll shop / distillery shop
KFEAT: Y = altar_kikubaaqudgha
SHUFFLE: ZY
: ossuary_setup_features(_G)
MAP
cccccccccccccccccccccc
cccc.....2..2.....cccc
ccc..2...cccc...2..ccc
cc......cc..cc......cc
cc.....cc....cc.....cc
cc........22........cc
ccc..3....22....3..ccc
cccc..............cccc
cccc+cc........cc+cccc
cccc^^cc111111cc^^cccc
ccc....cc....cc....ccc
ccd.....c....c.....dcc
c$dd....c....c....dd$c
c$dd....c....c....dd$c
c$$d....c....c....d$$c
cc$dd...c....c...dd$cc
cc$$d..cc....cc..d$$cc
ccc$d..cc....cc..d$ccc
cccZd.ccc.<A.ccc.dYccc
cccccccccccccccccccccc
ENDMAP

###########################################################
# Rare: WEIGHT 20
###########################################################

NAME:    minmay_ossuary_two_tombs
WEIGHT:  20
ORIENT:  encompass
TAGS:    no_item_gen no_monster_gen
# Loot: between 5 scrolls/potions and 9 scrolls/potions, with some gold.
# The tomb close to the player is a bit easier than the other one, and is
# more likely to be the one with less loot, giving weak players a chance.
SHUFFLE: def$
NSUBST:  d = 1:1 / *:.
NSUBST:  e = 1:1 / *:.
NSUBST:  $ = 1:1 / *:.
SUBST:   2 = 2 3:1 .:1
SUBST:   3 = 3 2:1 .:1
MONS:    mummy
MONS:    rat zombie / jackal zombie / quokka zombie / adder zombie / \
         goblin zombie / hobgoblin zombie / kobold zombie
MONS:    gnoll zombie / orc zombie / hound zombie / big kobold zombie / \
         human zombie / elf zombie / centaur zombie
KFEAT: Z = scroll shop / scroll shop / distillery shop
KFEAT: Y = altar_kikubaaqudgha
SHUFFLE: ZY
: ossuary_setup_features(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxx
x........<..A........x
x....................x
x......cccccccc......x
x...cccc3223+ecccc...x
x..cc.......ceec2cc..x
x..c<.ccccc.cccc..+..x
x..cc.+dddc.....2cc..x
x..2ccccdZc...cccc2..x
x2.....cccccccc.....2x
x..2....2.3.2.....2..x
x..2.....2.3.2....2..x
x2.....cccccccc.....2x
x..2cccc3+ffc3cccc2..x
x..cc2.2.cccc.c$$cc..x
x..+..........+$Y<c..x
x..cc2.2.cccc.c$$cc..x
x...cccc3+ffc3cccc...x
x......cccccccc......x
x.......2.3.2........x
x........2.3.2.......x
xxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# Scorpions, and lots of them. Hope you weren't expecting to breeze through
# the ossuary just because you found a mace of holy wrath!
NAME:   ossuary_minmay_scorpions
WEIGHT: 20
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen
MONS:   scorpion
MONS:   mummy
MONS:   gnoll zombie / hobgoblin zombie / orc zombie / \
        human zombie / kobold zombie / goblin zombie / \
        big kobold zombie / centaur zombie / scorpion zombie w:20
ITEM:   any scroll / any potion / any jewellery w:1 / nothing
KFEAT:  ~ = floor
SUBST:  1 = 1.
SUBST:  3 = 3.
KFEAT: Z = scroll shop / scroll shop / distillery shop
KFEAT: Y = altar_kikubaaqudgha
SHUFFLE: ZY
: ossuary_setup_features(_G)
MAP
ccccccccccccccccccccccccc
c...c..3c..1c~~~ndddc131c
c.A.c...G...+~~~nd2d+313c
c...c3......c~~~ndddc131c
cc+cccG...Gccc+ccccccc+cc
c...c......3c...c3.1c~~~c
c.<.+...G...c...G...c~~~c
c...c...c3..c......3c~~~c
ccccccccccccccG...Gcc~~~c
c1.1cdddn~~~c3.....3c~~~c
c.3.+d2Yn~~~+...G...c~~~c
c3.3cdddn~~~c..3c3.1c~~~c
cc+ccccccc+ccccccccccc+cc
c1...3c.....c~~~c...c3.1c
c.....c.....c~~~+...G...c
c.....G.....c~~~c......3c
c...........cc+cccG...Gcc
c3..........c3.3c3.....3c
cccG..3..Gccc...c...G...c
c3.........3c3.3c1.3c3.1c
c...........cc+ccccccc+cc
c.....G.....c~~~ndddc3.3c
c.....c.....+~~~nZ2d+.3.c
c3...3c3...1c~~~ndddc1.1c
ccccccccccccccccccccccccc
ENDMAP

# Another ossuary (loosely) based on Tomb:2
NAME:    ossuary_minmay_tomb_2a
WEIGHT:  20
ORIENT:  encompass
TAGS:    no_item_gen no_monster_gen no_rotate
MONS:    rat zombie / jackal zombie / goblin zombie / hobgoblin zombie / \
         adder zombie / gnoll zombie / orc zombie / kobold zombie / \
         big kobold zombie / hound zombie / human zombie / elf zombie
MONS:    mummy
ITEM:    any scroll / any scroll q:2, any potion / any potion q:2
SHUFFLE: $de*
NSUBST:  B = 1:+ / *:n
NSUBST:  C = 1:+ / *:n
NSUBST:  D = 1:+ / *:n
NSUBST:  E = 1:+ / *:n
KFEAT:   ~ = known needle trap / known arrow trap / known spear trap
SUBST:   ~ = ~.
NSUBST:  X = 1:< / *:.
KFEAT: Z = scroll shop / scroll shop / distillery shop
KFEAT: Y = altar_yredelemnul
SHUFFLE: ZY
: ossuary_setup_features(_G)
MAP
ccccccccccccccccccccccccccccccc
cZ$B~..........<..........~CdYc
c$$B~.....................~Cddc
cBBB~........ccccc........~CCCc
c~~~~.....................~~~~c
c....2.........1.........2....c
c.....cc....1.....1....cc.....c
c.....cc...............cc.....c
c.........1.........1.........c
c..c.......................c..c
c..cc..........X..........cc..c
c<.cc....1....XAX....1....cc.<c
c..cc..........X..........cc..c
c..c.......................c..c
c.........1.........1.........c
c.............................c
c.....cc....1.....1....cc.....c
c.....cc.......1.......cc.....c
c....2...................2....c
c~~~~.....................~~~~c
cEEE~........ccccc........~DDDc
c**E~.....................~Deec
cY*E~..........<..........~DeYc
ccccccccccccccccccccccccccccccc
ENDMAP

###########################################################
# Very rare: WEIGHT 10
###########################################################

# Players have two choices: they can try to hang back and slowly fight the
# undead, or they can run to the gold relying on their speed/stealth.
#
# The former option is difficult since there's no choke point, whereas the
# latter option is inherently risky.  Gold averages a bit less than 200.
#
# Originally this vault had the monsters behind doors and an alarm trap
# before the gold, but then I found out that zombies can't open doors anymore.
NAME:   minmay_ossuary_gauntlet
WEIGHT: 10
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen
NSUBST: 1 = 2:1 / *:2
MONS:   mummy
MONS:   goblin zombie / hobgoblin zombie / gnoll zombie / big kobold zombie / \
        orc zombie / human zombie w:2 / elf zombie w:2 / centaur zombie w:2
KFEAT: Z = scroll shop / scroll shop / distillery shop
: ossuary_setup_features(_G)
MAP
         ccccccccccccccccc
         c1c1c1c1c1c1c1c1c
  ccccc  c...............c
 cc...cc c...............c   ccccc
cc.....ccc...............cc cc$$$cc
c.........................ccc$$$$$c
c..<.A..................<....$$$$Zc
c.........................ccc$$$$$c
cc.....ccc...............cc cc$$$cc
 cc...cc c...............c   ccccc
  ccccc  c...............c
         c1c1c1c1c1c1c1c1c
         ccccccccccccccccc
ENDMAP

NAME:    ossuary_due_mausoleum
WEIGHT:  10
ORIENT:  encompass
TAGS:    no_item_gen no_monster_gen no_rotate no_vmirror
MONS:    orc skeleton / kobold skeleton / gnoll skeleton / orc skeleton
MONS:    mummy
MONS:    wraith
NSUBST:  = = 1:+ / *:c
KFEAT:   Z = scroll shop / scroll shop / distillery shop
KMONS:   Z = wraith
: ossuary_setup_features(_G)
MAP
ccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccc
cc1.1...................1.1cc
cc.<.....................<.cc
cc1.1....ccccc+ccccc....1.1cc
cc......cc.........cc......cc
cc.....cc...cc=cc...cc.....cc
cc....cc...cm...mc...cc....cc
cc....c...cm.ddd.mc...c....cc
cc....+...=.ddZdd.=...+....cc
cc....c...cm.ddd.mc...c....cc
cc....cc...cm...mc...cc....cc
cc.....cc...cc=cc...cc.....cc
cc......cc.........cc......cc
cc1.1....ccccc+ccccc....1.1cc
cc.<.....................<.cc
cc1.1...................1.1cc
ccccccccccccc+++ccccccccccccc
ccccccccccccc...ccccccccccccc
ccccccccccdd+.2.+ddcccccccccc
ccccccccccddc111cddcccccccccc
ccccccccccccc111ccccccccccccc
ccccccccccccc111ccccccccccccc
ccccccccccccc...ccccccccccccc
ccccccccccccc...ccccccccccccc
ccccccccccccc...ccccccccccccc
ccccccccccccc...ccccccccccccc
ccccccccccccc+++ccccccccccccc
cccccccccccc.....cccccccccccc
ccccccccccc.......ccccccccccc
cccccccc3o...<.A...o3cccccccc
ccccccccccc.......ccccccccccc
cccccccccccc.....cccccccccccc
ccccccccccccc...ccccccccccccc
ccccccccccccccocccccccccccccc
cccccccccccccc3cccccccccccccc
ccccccccccccccccccccccccccccc
ENDMAP

# Menkaure's pyramid
NAME:    ossuary_due_pyramid
WEIGHT:  10
ORIENT:  encompass
TAGS:    no_item_gen no_monster_gen no_rotate no_vmirror no_hmirror
MONS:    kobold zombie / gnoll zombie / orc zombie / rat zombie / \
         goblin zombie / hobgoblin zombie
MONS:    kobold skeleton / gnoll skeleton / orc skeleton / rat skeleton / \
         goblin skeleton / hobgoblin skeleton
MONS:    mummy
KMONS:   4 = Menkaure, mummy
# Loot: 1-16 items
ITEM:    any scroll / any potion / w:5 nothing
KFEAT:   ^ = floor
KFEAT:   m = known spear trap
KFEAT:   ~ = known alarm trap
NSUBST:  ' = 2:m / 25:1 / 25:2 / 30:^ / .
# 1KB: what's the point of these shortcuts?  Removed for now...
SUBST:    X : c
#SUBST:   X : XYc
#NSUBST:  X = 6:= / *:c
#NSUBST:  Y = 3:= / *:c
KFEAT:    Z = scroll shop / scroll shop / distillery shop
: ossuary_setup_features(_G)
MAP
                            cc
                           cccc
                          cccccc
                         cc''''cc
                        cc''cc''cc
                       cc''cccc''cc
                      cc''cXccXc''cc
                     cc''cX''''Xc''cc
                    cc''cX''cc''Xc''cc
                   cc''cX''cccc''Xc''cc
                  cc''cX''cXcccc''Xc''cc
                 cc''cX''cX''''Xc''Xc''cc
                cc''cX''cX''cc''Xc''Xc''cc
               cc''cX''cX''cccc''Xc''Xc''cc
              cc''cX''cX''ccdZcc''Xc''Xc''cc
             cc''cX''cX''ccddddcc''Xc''Xc''cc
            cc''cX''cX''ccd3dd3dcc''Xc''Xc''cc
           cc''cX''cX''cc.^d<<d..cc''Xc''Xc''cc
          cc''cX''cX''cc.^.4dd3^.^cc''Xc''Xc''cc
         cc''cX''cX''cc^..^.dd^.~.~cc''Xc''Xc''cc
        cc''cX''cX''cccccccccccccc++cc''Xc''Xc''cc
       cc''cX''cX''''''''''''''''''''cc''Xc''Xc''cc
      cc''cX''cccXcXcXcXcXcXcXcXcXcXcXcc''Xc''Xc''cc
     cc''cX''''''''''''''''''''''''''''''''Xc''Xc''cc
    cc''cccXcXcXcXcXcXcXcXcXcXcXcXcXcXcXcXcXcc''Xc''cc
   cc''''''''''''''''''''''''''''''''''''''''''''Xc<Acc
  cccccccccccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

NAME:   ossuary_zaba_two_paths
# There is no way up in the end. This is to give the zombies in the beginning
# more time to go for a walk and another chance to meet the player.
ORIENT: encompass
WEIGHT: 10
TAGS:   no_item_gen no_monster_gen no_rotate
#
# Standard monsters: small zombies, large zombies, boss:
MONS:   kobold zombie / goblin zombie / orc zombie / hobgoblin zombie
MONS:   big kobold zombie / gnoll zombie
MONS:   mummy
#
KFEAT:  F = floor
COLOUR: F = cyan
KMONS:  F = rat simulacrum / goblin simulacrum / kobold simulacrum /\
            bat simulacrum / nothing w:80
KFEAT:  S = floor
COLOUR: S = green
KMONS:  S = spectral rat / spectral goblin / spectral kobold /\
            spectral bat / nothing w:80
# Loot.
ITEM:   any potion / any scroll / nothing w:1
#
# There are not always traps in corridors but they are guaranteed near the loot:
KFEAT:  ' = known alarm trap w:15 / floor w:85
KFEAT:  " = known spear trap w:1 / known arrow trap w:15
KFEAT:  Z = scroll shop / scroll shop / distillery shop
#
: ossuary_setup_features(_G)
MAP
ccccccccccccccccccccccccccc
c2c2c2cc...........cc2c2c2c
c+c+c+c.............c+c+c+c
c..........G...G..........c
c.........................c
c+c+ccccccccc+ccccccccc+c+c
bScc'ccG2+.......+2Gcc'ccFv
bSScc'ccGc.......cGcc'ccFFv
bSSScc'ccc.12321.ccc'ccFFFv
bSSSScc'cc.......cc'ccFFFFv
bSSSSScc'c.......c'ccFFFFFv
bSSSSSS+'c...G...c'+FFFFFFv
bSSSSScc'c.......c'ccFFFFFv
bSSSScc'cc.""""".cc'ccFFFFv
bSSScc'ccc."ddd".ccc'ccFFFv
bSScc'ccGc."ddd".cGcc'ccFFv
bScc'ccG2+."dZd".+2Gcc'ccFv
ccc+ccccccccccccccccccc+ccc
c11111cc1c1c1c1c1c1cc11111c
c......c+c+c+c+c+c+c......c
c.........................c
c..........<.A.<..........c
ccccccccccccccccccccccccccc
ENDMAP

# A larger ossuary than usual: there is a corridor with some monsters (and
# chances of wights) to get past, and then a little treasure room at the end.
#
# A door in the treasure room leads to another corridor flanked with a wraith
# (a hint of what's to come) on one side, and a flying skull on the other:
# the flying skull is basically an intelligent alarm trap, both behind glass.
#
# At the end of the corridor is a second treasure room, this one also
# containing more flying skeleton alarm traps, plus wraiths inside 3x3 rooms.
#
NAME:    ossuary_due_cavern
WEIGHT:  10
ORIENT:  encompass
TAGS:    no_item_gen no_monster_gen
MONS:    centaur zombie w:1 / orc zombie / hobgoblin zombie /\
         kobold zombie / adder zombie / orc skeleton / hobgoblin skeleton /\
         ball python skeleton / nothing w:5
MONS:    wight
MONS:    flying skull
MONS:    wraith
ITEM:    any scroll / any potion
ITEM:    any scroll / any potion / any wand w:4
KMONS:   V = wraith
KMONS:   X = flying skull
SHUFFLE: VX
SUBST:   x = c:20 n:8
SUBST:   z = c:20 n:4
SUBST:   y = c:20 n:1
SUBST:   $ = 1:10 2:1
NSUBST:  4 = 2:4 / *:2
KFEAT:   Z = scroll shop / scroll shop / distillery shop
: ossuary_setup_features(_G)
MAP
  ccccccccc
 cc.......cc
cc.........c
c...<..A..cc
cc.......cc
 ccc.....cc
   cc...cc
    cc.ccc
     cc.cc
     cc.cc
     cc.cc
    cc.cc
   cc.cc                            ccccccccccccccccc
   cc..ccc                          c...............c
    cc...cc                         c.+cc..xxx..cc+.c
  ccc...ccc                         c.c4c..x3x..c4c.c
 cc......cccccccccccccccccccccccccccc.ccc..yzy..ccc.c
cc........cc$c$c$c$c$cc.....cccccVccc...............c
c..........c+c+c+c+c+cc.GdG.cccccoccc.cxy..eee..yxx.c
c..c.......+..........+..d....+.....+.c3z..eZe..z3x.c
cc.cc......c+c+c+c+c+cc.GdG.cccccoccc.cxy..eee..yxx.c
cc.c......cc$c$c$c$c$cc.....cccccXccc...............c
cc.cc....cccccccccccccccccccccccccccc.ccc..yzy..ccc.c
 cc.....cc                          c.c4c..x3x..c4c.c
  cc...cc                           c.+cc..xxx..cc+.c
   ccccc                            c...............c
                                    ccccccccccccccccc
ENDMAP


# Four chambers, one with loot and the exit, three with zombies and a mummy.
# (The player won't know which is which because Crawl rotates the vault.)
#
# Stealthy players can close the doors without the mummy noticing. This is
# probably okay, but may turn out to be too easy.... -- dpeg
NAME:   ossuary_minmay_four_chambers
WEIGHT: 10
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen
MONS:   human zombie / elf zombie / centaur zombie
MONS:   mummy
KFEAT:  Z = scroll shop / scroll shop / distillery shop
: ossuary_setup_features(_G)
MAP
            ccc
          cccZccc
         cc.....cc
         c.......c
         c.......c
         c.......c
         cc.....cc
          ccc.ccc
            c+c
  ccccc   ccc.ccc   ccccc
 cc1.1cc cc.....cc cc1.1cc
 c.....c c.......c c.....c
cc.....ccc...<...ccc.....cc
c2......+...<A<...+......2c
cc.....ccc...<...ccc.....cc
 c.....c c.......c c.....c
 cc1.1cc cc.....cc cc1.1cc
  ccccc   ccc.ccc   ccccc
            c+c
          ccc.ccc
         cc.....cc
         c1.....1c
         c.......c
         c1.....1c
         cc.....cc
          ccc2ccc
            ccc
ENDMAP

NAME:   ossuary_kennysheep_city_of_the_dead
WEIGHT: 10
ORIENT: encompass
TAGS:   no_item_gen no_monster_gen
KMONS:  1W = wraith
KMONS:  2 = mummy
KMONS:  3 = goblin zombie / hobgoblin zombie / kobold zombie / gnoll zombie w:2 /\
        big kobold zombie w:1 / human zombie w:1 / elf zombie w:1 / orc zombie w:1 /\
                nothing w:36
KITEM:  12d = nothing
NSUBST: d = 1:1 / 3:2 / 1:Z / *:.
KFEAT:  Z = scroll shop / scroll shop / distillery shop
: ossuary_setup_features(_G)
MAP
ccccccccccccccccccccccccccccc
c...........................c
c...........................c
c..xxx..xxx..xxx..xxx..xxx..c
c..xdx..xdx..xdx..xdx..xdx..c
c..x+x..x+x..x+x..x+x..x+x..c
c..x.x..x.x..x.x..x.x..x.x..c
c...........................c
c...........................c
c..xxxx..3...xxx...3..xxxx..c
c..xd+..3G3..xdx..3G3..+dx..c
c..xxxx..3...x+x...3..xxxx..c
c............x.x............c
c...........................c
c..xxxx.xxxx..3..xxxx.xxxx..c
c..xd+..xd+..3G3..+dx..+dx..c
c..xxxx.xxxx..3..xxxx.xxxx..c
c...........................c
c............x.x............c
c..xxxx..3...x+x...3..xxxx..c
c..xd+..3G3..xdx..3G3..+dx..c
c..xxxx..3...xxx...3..xxxx..c
c...........................c
c...........................c
c..x.x..x.x..x.x..x.x..x.x..c
c..x+x..x+x..x+x..x+x..x+x..c
c..xdx..xdx..xdx..xdx..xdx..c
c..xxx..xxx..xxx..xxx..xxx..c
c...........................c
c...........................c
ccccccccccccc+++ccccccccccccc
           cc...cc
          cc.....cc
       cccc.......cccc
       cWn...<.A...nWc
       cccc.......cccc
          cc.....cc
           cc...cc
            ccncc
             cWc
             ccc
ENDMAP
